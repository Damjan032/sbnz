package rules;

import com.sbnz.adsys.utils.Constants;
import java.time.LocalDate;

import com.sbnz.adsys.model.Candidate;
import com.sbnz.adsys.model.AdvertisementRequest;
import com.sbnz.adsys.model.Advertisement;
import com.sbnz.adsys.model.Tag;
import com.sbnz.adsys.model.SocialMediaPage;
import com.sbnz.adsys.model.SocialMediaUser
import java.time.Duration;

global Double COMPANY_COEFFICIENT;
global Double CLICKED_ADS_COEFFICIENT;
global Double IGNORED_ADS_COEFFICIENT;
global Double LIKED_PAGES_COEFFICIENT;
global Double HERD_COEFFICIENT;


rule "Target Group Age" salience 10
lock-on-active
    when
        $req: AdvertisementRequest($min: minAge, $max: maxAge)
        $c : Candidate(user.age >= $min, user.age <= $max)
    then
        modify ($c) {setAgeGroup(true)}
end


rule "Target Group Geography" salience 10
lock-on-active
    when
        $req: AdvertisementRequest($query: geographicLocation)
        $c: Candidate(user.fullLocation.contains($query))
    then
        modify ($c) {setGeographyGroup(true)}
end


rule "Is Target Group" salience 9
    when
        $c: Candidate(ageGroup && geographyGroup) @Watch(ageGroup, geographyGroup)
    then
        modify ($c) {setTargetGroup(true)}
end


rule "Is Not Target Group" salience 9
    when
        $c: Candidate(!(ageGroup && geographyGroup))
    then
        System.out.println("Candidate with name: " + $c.getUser().fullName() + " does NOT belong to ad target group!");
end


rule "5+ Ads Ignored this Month by the Same Company" salience 8
    when
        $req: AdvertisementRequest($currentAd: advertisement, $currentAdvertiser: advertisement.advertiser)
        $c: Candidate(targetGroup)  @Watch(targetGroup)
        Number(intValue >= 5) from accumulate(
            Advertisement(
                advertiser == $currentAdvertiser &&
                datePublished.isAfter(LocalDate.now().minusMonths(1))
            ) from $c.user.ignoredAdvertisements,
            count()
        )
    then
        System.out.println("Candidate: " + $c.getUser().fullName() + " has ignored 5+ ads by "
        + $currentAdvertiser.getName() + " and he's eliminated from the candidate pool!");
        modify ($c){setDisqualified(true)}
end


rule "5+ Ads Clicked on this Month by the Same Company" salience 7
    when
        $req: AdvertisementRequest($currentAd: advertisement, $currentAdvertiser: advertisement.advertiser)
        $c: Candidate(targetGroup && !disqualified)
        Number(intValue >= 5) from accumulate(
            Advertisement(
                advertiser == $currentAdvertiser &&
                datePublished.isAfter(LocalDate.now().minusMonths(1))
            ) from $c.user.clickedAdvertisements,
            count()
        )
    then
        System.out.println("Candidate: " + $c.getUser().fullName() + " has clicked on 5+ ads by "
        + $currentAdvertiser.getName() + " and he will be automatically qualifed!");
        modify ($c) {setAutomaticallyQualified(true)}
end


rule "Is In Pool" salience 6
    when
        $c: Candidate(targetGroup && !disqualified && !automaticallyQualified, $user: user)
    then
end


rule "Candidate and Company Interaction Coefficient" extends "Is In Pool" salience 5
    when
        $req: AdvertisementRequest($currentAdvertiser: advertisement.advertiser)
        Number($positiveCoefficient: doubleValue) from accumulate (
            Advertisement(advertiser == $currentAdvertiser) from $user.clickedAdvertisements,
            count()
        )
        Number($negativeCoefficient: doubleValue) from accumulate (
            Advertisement(advertiser == $currentAdvertiser) from $user.ignoredAdvertisements,
            count()
        )
        eval($positiveCoefficient + $negativeCoefficient > 0)
    then
        double clickRatio = $positiveCoefficient / ($positiveCoefficient + $negativeCoefficient);
        modify ($c) {setAdsByCompanyCoefficient(clickRatio)}
end


rule "Calculate Clicked Ads Coefficient" extends "Is In Pool" salience 5
    when
        $req: AdvertisementRequest($currentAd: advertisement)
        Number($clickCoeff: doubleValue) from accumulate (
            Tag($singleTag: this) from $currentAd.tags and
            Advertisement(tags contains $singleTag, $ad: this) from $user.clickedAdvertisements,
            init(double cnt = 0;),
            action({
                double differenceInDays = Duration.between($currentAd.getDatePublished().atStartOfDay(),
                                            $ad.getDatePublished().atStartOfDay()).toDays();
                cnt += 1 / Math.max(Math.abs(differenceInDays), 1);
            }),
            result(cnt)
        )
    then
        modify ($c) {setClickedAdsCoefficient($clickCoeff)}
end


rule "Calculate Ignored Ads Coefficient" extends "Is In Pool" salience 5
     when
         $req: AdvertisementRequest($currentAd: advertisement)
         Number($ignoreCoeff: doubleValue) from accumulate (
            Tag($singleTag: this) from $currentAd.tags and
            Advertisement(tags contains $singleTag, $ad: this) from $user.ignoredAdvertisements,
            init(double cnt = 0;),
            action({
                double differenceInDays = Duration.between($currentAd.getDatePublished().atStartOfDay(),
                                            $ad.getDatePublished().atStartOfDay()).toDays();
                cnt -= 1 / Math.max(Math.abs(differenceInDays), 1);
            }),
            result(cnt)
         )
     then
        modify ($c) {setIgnoredAdsCoefficient($ignoreCoeff)}
end


rule "Calculate Liked Pages Coefficient" extends "Is In Pool" salience 5
     when
         $req: AdvertisementRequest($currentAd: advertisement)
         Number($pageCoeff: doubleValue) from accumulate (
             Tag($singleTag: this) from $currentAd.tags and
             SocialMediaPage(categoryKeywords contains $singleTag) from $user.likedSocialMediaPages,
             count()
         )
     then
        modify ($c) {setLikedPagesCoefficient($pageCoeff)}
end


rule "Calculate Initial Score" extends "Is In Pool" salience 4
    when
    then
        double score = $c.getAdsByCompanyCoefficient() * COMPANY_COEFFICIENT +
            $c.getClickedAdsCoefficient() * CLICKED_ADS_COEFFICIENT +
            $c.getIgnoredAdsCoefficient() * IGNORED_ADS_COEFFICIENT +
            $c.getLikedPagesCoefficient() * LIKED_PAGES_COEFFICIENT;

        modify ($c) {setInitialScore(score)}
end


rule "Calculate Herd Coefficient" extends "Is In Pool" salience 3
    when
        Number($herdCoeff: doubleValue) from accumulate (
            SocialMediaPage($userWhoLikeThePage: usersWhoLikeThePage) from $user.likedSocialMediaPages and
            Candidate($userWhoLikeThePage contains user, user != $user,
                targetGroup && !disqualified && !automaticallyQualified, $candidateScore: initialScore),
            average($candidateScore)
        )
    then
        modify ($c) {setHerdCoefficient($herdCoeff)}
end


rule "Add Herd Coefficient to Final Score" extends "Is In Pool" salience 2
    when
    then
        double finalScore = $c.getInitialScore() + HERD_COEFFICIENT * $c.getHerdCoefficient();
        modify ($c) {setFinalScore(finalScore)}
end


rule "Set Final Score for Automatically Qualified Candidates" salience 2
    when
        $c: Candidate(targetGroup && automaticallyQualified)
    then
        modify ($c) {setFinalScore(Double.POSITIVE_INFINITY)}
end


rule "Set Final Score for Disqualified Candidates" salience 2
    when
        $c: Candidate(targetGroup && disqualified)
    then
        modify ($c) {setFinalScore(Double.NEGATIVE_INFINITY)}
end